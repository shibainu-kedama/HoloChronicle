extends Control

@onready var node_container = $ScrollContainer/ViewportContent/NodeContainer
@onready var path_drawer = $ScrollContainer/ViewportContent/PathDrawer
@onready var background = $ScrollContainer/ViewportContent/Background
@onready var deck_button := $StatusPanel/DeckViewButton
@onready var hp_label := $StatusPanel/HPLabel
@onready var gold_label := $StatusPanel/GoldLabel

func _ready():
	load_node_types_from_csv("res://data/map_nodes.csv")
	Global.node_links.clear()
	load_paths_from_csv("res://data/map_paths.csv")

	# â† æœ€åˆã®é¸æŠå¯èƒ½ãƒãƒ¼ãƒ‰
	if Global.unlocked_nodes.is_empty():
		Global.unlocked_nodes = ["1-A", "1-B", "1-C"]  # â†æœ€åˆã¯å…¨éƒ¨é–‹æ”¾
	update_node_interactability()
	deck_button.pressed.connect(_on_deck_view_pressed)
	update_status_display()

func update_status_display():
	hp_label.text = "â¤ï¸ HP: %d / %d" % [Global.player_hp, Global.player_max_hp]
	gold_label.text = "ğŸ’° Gold: %d" % Global.player_gold

func _on_deck_view_pressed():
	var popup = preload("res://scenes/DeckPopup.tscn").instantiate()
	add_child(popup)
	popup.show_cards(Global.player_deck)
	popup.visibility_changed.connect(func(): if not popup.visible: popup.queue_free())

# ãƒãƒ¼ãƒ‰ç¨®åˆ¥ã‚’CSVã‹ã‚‰èª­ã¿è¾¼ã¿ã€ãƒ©ãƒ³ãƒ€ãƒ ã«ã‚»ãƒƒãƒˆ
func load_node_types_from_csv(path: String) -> void:
	var file = FileAccess.open(path, FileAccess.READ)
	if not file:
		push_error("CSVãƒ•ã‚¡ã‚¤ãƒ«èª­ã¿è¾¼ã¿å¤±æ•—: %s" % path)
		return

	file.get_csv_line() # ãƒ˜ãƒƒãƒ€ãƒ¼ã‚’èª­ã¿é£›ã°ã™

	while not file.eof_reached():
		var cols: PackedStringArray = file.get_csv_line()
		if cols.size() < 2:
			continue

		var node_id = String(cols[0]).strip_edges()
		if node_id == "":
			continue

		var chosen_type := ""
		if Global.node_types.has(node_id):
			chosen_type = String(Global.node_types[node_id])

		var type_candidates: Array[String] = []
		for raw_type in cols.slice(1, cols.size()):
			var t = String(raw_type).strip_edges().to_lower()
			if t != "":
				type_candidates.append(t)

		if chosen_type == "":
			if type_candidates.is_empty():
				continue
			# shop å€™è£œã‚’å«ã‚€ãƒãƒ¼ãƒ‰ã¯ shop ã‚’å„ªå…ˆ
			chosen_type = "shop" if type_candidates.has("shop") else type_candidates[randi() % type_candidates.size()]
			Global.node_types[node_id] = chosen_type

		var map_node = node_container.get_node_or_null(node_id)
		if map_node and map_node.has_method("set_type"):
			map_node.set_type(chosen_type)
		else:
			print("ãƒãƒ¼ãƒ‰ID %s ãŒå­˜åœ¨ã—ãªã„ã‹ set_type ãŒæœªå®šç¾©ã§ã™" % node_id)

# ãƒãƒ¼ãƒ‰æ¥ç¶šã‚’CSVã‹ã‚‰èª­ã¿è¾¼ã¿ã€ç·šã§ã¤ãªã
func load_paths_from_csv(path: String) -> void:
	var file = FileAccess.open(path, FileAccess.READ)
	if not file:
		push_error("CSVãƒ•ã‚¡ã‚¤ãƒ«èª­ã¿è¾¼ã¿å¤±æ•—: %s" % path)
		return

	file.get_line()  # ãƒ˜ãƒƒãƒ€ãƒ¼ã‚’èª­ã¿é£›ã°ã™

	while not file.eof_reached():
		var line = file.get_line().strip_edges()
		if line == "":
			continue

		var cols = line.split(",")
		if cols.size() < 2:
			continue

		var from_id = cols[0]
		var to_id = cols[1]
		
		# æ¥ç¶šæƒ…å ±ã‚’ä¿å­˜
		# Global.node_links ã‚’ã‚»ãƒƒãƒˆ
		if not Global.node_links.has(from_id):
			Global.node_links[from_id] = []
		Global.node_links[from_id].append(to_id)

		# ç·šã‚’å¼•ã
		var from_node = node_container.get_node_or_null(from_id)
		var to_node = node_container.get_node_or_null(to_id)

		if from_node and to_node:
			draw_line_between_nodes(from_node, to_node)
		else:
			print("ãƒãƒ¼ãƒ‰ãŒè¦‹ã¤ã‹ã‚Šã¾ã›ã‚“: %s â†’ %s" % [from_id, to_id])

# 2ã¤ã®ãƒãƒ¼ãƒ‰ã®ä¸­å¿ƒä½ç½®ã‚’ç·šã§ã¤ãªã
func draw_line_between_nodes(node_a: Control, node_b: Control) -> void:
	var start_pos = node_a.global_position + node_a.size * 0.5
	var end_pos = node_b.global_position + node_b.size * 0.5

	var line = Line2D.new()
	line.width = 4
	line.default_color = Color.WHITE
	line.add_point(start_pos)
	line.add_point(end_pos)

	path_drawer.add_child(line)

func update_node_interactability():
	print("UIæ›´æ–°ä¸­:", Global.unlocked_nodes)
	for node in node_container.get_children():
		if node is Button:
			var node_name = node.name
			var is_unlocked = Global.is_node_unlocked(node_name)
			var is_passed = Global.passed_nodes.has(node_name)

			# æŠ¼ã›ã‚‹ã‹ã©ã†ã‹ã‚’åˆ¶å¾¡
			node.disabled = not is_unlocked or is_passed

			# é€šéæ¸ˆã¿ã¯ã‚°ãƒ¬ãƒ¼è¡¨ç¤º
			if node.has_method("set_passed_visual"):
				node.set_passed_visual(is_passed)

			# ğŸ”¸æ³¨ç›®ã‚¢ãƒ‹ãƒ¡ãƒ¼ã‚·ãƒ§ãƒ³ã®å†ç”Ÿï¼šæ¬¡ã«é€²ã‚ã‚‹ãƒãƒ¼ãƒ‰ã®ã¿
			if is_unlocked and not is_passed:
				if node.has_method("play_attention_animation"):
					node.play_attention_animation()
